buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.gradle.plugin-publish' version '0.9.6'
    id 'com.jfrog.bintray' version '1.7.3'
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'
apply from: 'mvn-push.gradle'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy:2.4.7'
    compile 'org.marketcetera.fork:commons-csv:2.4.2'
    compile 'org.apache.poi:poi-ooxml-schemas:3.15'
    compile 'org.apache.poi:poi-ooxml:3.15'
    compile 'org.apache.poi:poi:3.15'
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.6.0'
    testCompile 'org.xmlunit:xmlunit-core:2.3.0'
}

group = GROUP
version = VERSION_NAME

bintray {
    user = POM_DEVELOPER_ID
    key = BINTRAY_API_KEY
    pkg {
        repo = 'maven'
        name = POM_ARTIFACT_ID
    }
}

pluginBundle {
    website = POM_URL
    vcsUrl = POM_SCM_URL
    description = POM_DESCRIPTION
    tags = ['localization', 'android']

    plugins {
        localizationPlugin {
            id = 'pl.droidsonroids.localization'
            displayName = POM_NAME
            description = POM_DESCRIPTION
        }
    }
}

jacoco {
    toolVersion = '0.7.8'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_7

uploadArchives.dependsOn check

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
    group = 'verification'
    description = 'To be used in Android Studio, workaround for https://code.google.com/p/android/issues/detail?id=64887'
}