buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'groovy'
    id 'maven'
    id 'signing'
    id 'jacoco'
}

apply from: 'mvn-push.gradle'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation 'org.codehaus.groovy:groovy:2.5.4'
    implementation 'org.marketcetera.fork:commons-csv:3.0.12'
    implementation 'org.apache.poi:poi-ooxml-schemas:4.0.1'
    implementation 'org.apache.poi:poi-ooxml:4.0.1'
    implementation 'org.apache.poi:poi:4.0.1'
    implementation 'org.apache.poi:ooxml-schemas:1.4'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.xmlunit:xmlunit-core:2.6.2'
}

group = GROUP
version = VERSION_NAME

pluginBundle {
    website = POM_URL
    vcsUrl = POM_SCM_URL
    description = POM_DESCRIPTION
    tags = ['localization', 'android']

    plugins {
        localizationPlugin {
            id = 'pl.droidsonroids.localization'
            displayName = POM_NAME
            description = POM_DESCRIPTION
        }
    }
}

jacoco {
    toolVersion = '0.7.8'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_7

uploadArchives.dependsOn check

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
    group = 'verification'
    description = 'To be used in Android Studio, workaround for https://code.google.com/p/android/issues/detail?id=64887'
}